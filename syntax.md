<程序> ::= <分程序>.
<分程序> ::= [<类型说明部分>][<常量说明部分>][<变量说明部分>][<模块说明部分>]<语句>
<类型说明部分> ::= <重定义类型> | <枚举类型> | <结构体类型>
<重定义类型> ::= TYPE <标识符>=<标识符>;
<枚举类型> ::= TYPE <标识符>='{' <标识符> {, <标识符>} '}';
<结构体类型> ::= TYPE <标识符>=RECORD; <变量及类型>{; <变量及类型>}; END;
<常量说明部分> ::= CONST <常量定义> {, <常量定义>};
<常量定义> ::= <标识符>=<字面量>
<字面量> ::= <有符号整数>|<有符号小数>|<字符串常量>
<变量说明部分> ::= VAR <变量及类型>{, <变量及类型>};
<变量及类型> ::= <标识符>['['<区间说明>{, <区间说明>}']'] : <类型>
<区间说明> ::= <整数或常量> : <整数或常量>
<模块说明部分> ::= (<过程说明部分> | <函数说明部分>){; <模块说明部分>};
<过程说明部分> ::= <过程说明首部><分程序>
<函数说明部分> ::= <函数说明首部><分程序>
<过程说明首部> ::= PROCEDURE<标识符>(<参数列表>);
<函数说明首部> ::= FUNCTION<标识符>(<参数列表>):<类型>;
<参数列表> ::= (<变量及类型> {, <变量及类型>})|<空>
<语句> ::= <赋值语句>|<条件语句>|<循环语句>|<过程调用语句>|<读语句>|<写语句>|<复合语句>|<返回语句>|<空>
<赋值语句> ::= <左值表达式>:=<表达式>
<左值表达式> ::= <标识符>['['<区间表达式>'{,<区间表达式>}']']{. <左值表达式>}
<复合语句> ::= BEGIN<语句>{; <语句>}END
<条件表达式> ::= <或条件> {OR <或条件>}
<或条件> ::= <条件> {AND <条件>}
<条件> ::= <表达式><关系运算符><表达式>|ODD<表达式>
<表达式> ::= [+|-]<项>{<加法运算符><项>}
<项> ::= <因子>{<乘法运算符><因子>}
<因子> ::= <左值表达式>|<字面量>|'(' <表达式> ')'|<调用语句>
<加法运算符> ::= +|-
<乘法运算符> ::= *|/|MOD|%
<关系运算符> ::= =|#|<|<=|>|>=|!=
<条件语句> ::= <IF条件语句> | <CASE条件语句>
<IF条件语句> ::= IF<条件表达式>THEN<语句>{ELSEIF<条件表达式>THEN<语句>}[ELSE<语句>]
<CASE条件语句> ::= CASE <表达式> OF <CASE从句>{; <CASE从句>} [; <ELSE从句>]; END
<调用语句> ::= [CALL]<标识符>'(' [<表达式> {,<表达式>}]')'
<循环语句> ::= <当型循环语句>|<直到型循环语句>|<FOR循环语句>
<当型循环语句> ::= WHILE<条件表达式>DO<语句>
<直到型循环语句> ::= REPEAT <语句> UNTIL <条件表达式>
<FOR循环语句> ::= FOR <左值表达式> := <表达式> STEP <表达式> UNTIL <表达式> DO <语句>
<读语句> ::= READ'('<左值表达式>{, <左值表达式>}')'
<写语句> ::= WRITE'('<表达式>{, <表达式>}')'
<返回语句> ::= RETURN <表达式>
<字母> ::= a-zA-Z
<数字> ::= 0-9
<字符串常量> ::= "<非转义引号外任意字符>"
<注释> ::= <行注释> | <块注释>
<行注释> ::= // <任意字符> <换行符>
<块注释> ::= /* <任意字符> */
